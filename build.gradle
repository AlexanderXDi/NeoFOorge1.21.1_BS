plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.71'
}

// Load local.properties if it exists
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
    localProperties.each { prop ->
        project.extensions.extraProperties.set(prop.key, prop.value)
    }
}

version = mod_version
group = mod_group_id

base {
    archivesName = mod_name
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = neoforge_version

    parchment {
        mappingsVersion = '2024.07.28'
        minecraftVersion = '1.21'
    }

    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    maven { url = "https://maven.minecraftforge.net/" }
}

dependencies {
    implementation "net.neoforged:neoforge:${"21.11.38-beta"}"
    implementation "com.github.glitchfiend:TerraBlender-neoforge:1.21.1-4.1.0.8"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            neoforge_version       : neoforge_version,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
            mod_license            : mod_license
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
        expand replaceProperties
    }
}

tasks.withType(ProcessResources).configureEach {
    filteringCharset = 'UTF-8'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

// Custom task for local development to copy JAR to mods folder
tasks.register('copyJar', Copy) {
    dependsOn 'jar'
    
    // Check local.properties or gradle.properties for mod_dest_dir
    def destDir = project.findProperty('mod_dest_dir')
    
    if (destDir) {
        from jar.archiveFile
        into destDir
        
        doLast {
            println "Successfully copied ${jar.archiveFileName.get()} to ${destDir}"
        }
    }
}

// Make build task depend on copyJar so it runs automatically
tasks.named('build') {
    finalizedBy 'copyJar'
}
